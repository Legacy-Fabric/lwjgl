plugins {
    id "c"
    id 'maven-publish'
}

sourceSets {
    common {
        resources {
            srcDir "lwjgl-platform/src/common/c"
        }
    }
    linux {
        resources {
            srcDir "lwjgl-platform/src/linux/c"
        }
    }
    macosx {
        resources {
            srcDir "lwjgl-platform/src/macosx/c"
        }
    }
    windows {
        resources {
            srcDir "lwjgl-platform/src/windows/c"
        }
    }
    generated {
        resources {
            srcDir "lwjgl-platform/src/generated/c"
        }
    }
}

tasks.register("linuxNativeJar", Jar) {
    dependsOn ":lwjgl:compileJava"

    manifest {
        attributes(
                "Implementation-Version": project.version
        )
    }

    from fileTree("../bin/lwjgl") {
        include "liblwjgl*.so"
    }
    from fileTree("libs/linux") {
        include "libopenal*.so"
    }

    archiveClassifier.set("natives-linux")
    destinationDirectory.set(file("build/libs"))
    archiveBaseName.set("lwjgl-platform")
}

tasks.register("macosxNativeJar", Jar) {
    dependsOn ":lwjgl:compileJava"

    manifest {
        attributes(
                "Implementation-Version": project.version
        )
    }

    from fileTree("../bin/lwjgl") {
        include "liblwjgl*.dylib"
    }
    from fileTree("libs/macosx") {
        include "openal*.dylib"
    }

    archiveClassifier.set("natives-osx")
    destinationDirectory.set(file("build/libs"))
    archiveBaseName.set("lwjgl-platform")
}

tasks.register("windowsNativeJar", Jar) {
    dependsOn ":lwjgl:compileJava"

    manifest {
        attributes(
                "Implementation-Version": project.version
        )
    }

    from fileTree("../bin/lwjgl") {
        include "lwjgl*.dll"
    }
    from fileTree("libs/windows") {
        include "OpenAL*.dll"
    }

    archiveClassifier.set("natives-windows")
    destinationDirectory.set(file("build/libs"))
    archiveBaseName.set("lwjgl-platform")
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId "lwjgl-platform"

            artifact(linuxNativeJar) {
                builtBy linuxNativeJar
            }
            artifact(windowsNativeJar) {
                builtBy windowsNativeJar
            }
            artifact(macosxNativeJar) {
                builtBy macosxNativeJar
            }

            pom {
                name = "Lighweight Java Game Library - Platform"
                description = "Lighweight Java Game Library - Platform"
                url = "http://lwjgl.org/"

                scm {
                    url = "http://java-game-lib.svn.sourceforge.net/viewvc/java-game-lib/trunk/"
                    connection = "https://java-game-lib.svn.sourceforge.net/svnroot/java-game-lib"
                }
                withXml {
                    def parenting = asNode().appendNode("parent")
                    parenting.appendNode("groupId", project.group)
                    parenting.appendNode("artifactId", "parent")
                    parenting.appendNode("version", project.version)
                }
            }
        }
    }
    setupRepositories(repositories)
}